import System
import System.Net
import System.Globalization
import System.Diagnostics
import System.Collections.Generic
import System.Linq.Enumerable
import System.Xml.Linq
import System.Xml.XPath.Extensions from System.Xml.Linq
import System.IO
import System.Net.Mail
import Mono.Cecil

class Project:
	_project as string
	_projectFile as string
	_isX86 as bool
	_frameworkVersion as string

	property BuildOutput as string
	property TestOutput as string

	def constructor(projectFile as string, isX86 as bool, frameworkVersion as string):
		_projectFile = projectFile
		_project = Path.GetFileNameWithoutExtension(projectFile)
		_isX86 = isX86
		_frameworkVersion = frameworkVersion

	def Build():
		clean = MsBuild(_projectFile,
			Target : "clean",
			Parameters : { "Configuration" : "debug" },
			FrameworkVersion : _frameworkVersion)
		clean.Arguments.Add("/verbosity:minimal")

		build = MsBuild(_projectFile,
			Target : "build",
			Parameters : { "Configuration" : "debug" },
			FrameworkVersion : _frameworkVersion)
		build.Arguments.Add("/verbosity:minimal")
		clean.Execute()
		build.Execute()

		BuildOutput = clean.Output.ToString() + "\r\n" + build.Output.ToString()

	def Test():
		assembly = Path.Combine(Path.GetDirectoryName(_projectFile), "bin/debug/${_project}.dll")
		assemblyDefinition = AssemblyDefinition.ReadAssembly(assembly)
		_isX86 = assemblyDefinition.MainModule.Architecture == TargetArchitecture.I386

		nunitPath = """nunit-console.exe"""
		if _isX86:
			nunitPath = """nunit-console-x86.exe"""
		nunit = NUnit("\"" + assembly + "\"",
			ExecutablePath : nunitPath)

		nunit.Execute()

		TestOutput = nunit.Output.ToString()

	def ToString():
		return _project

frameworkVersion = Globals.FrameworkVersion

def GetProjects():
	isx86 = Globals.Maybe.platform == "x86" or Globals.Maybe.Platform == "x86"
	files = FileSet(["src/**/*Test.*proj",\
		"src/**/*Tests.*proj",\
		"src/**/Functional.*proj",\
		"src/**/Integration.*proj",\
		"src/**/Test.*proj",\
		"src/**/Unit.*proj"],
		Excludes: ["**/Test.Support*.*proj"]).Files
	return files.Select({f| Project(f, isx86, frameworkVersion)}).ToList()

task @integration, [@test]

task @test, [@RunTests]

task @PrepareBuild, [@CheckWritingErrors, "packages:install"]:
	if Exist("build/version.txt"):
		Engine.Execute("GenerateAssemblyInfo")

desc """
Если сказать CreateIgnoreFiles=True, 
будут созданы файлы игнора,
либо просто запущен процесс поиска ошибок
"""
task @CheckWritingErrors:
	creareIgnore = Configuration.Maybe.CreateIgnoreFiles or Configuration.Maybe.CreateIgnoreFiles
	if (creareIgnore):
		Exec("SearchOfWriting", "CreateIgnoreFiles:" + creareIgnore).Execute()
	else:
		Exec("SearchOfWriting").Execute()
		
task @BuildTests, [@PrepareBuild]:
	projects = GetProjects()
	for project in projects:
		project.Build()

task @TestJs:
	return unless Exist("test")
	if Exist("Gruntfile.js"):
		Sh("grunt")
		return

	root = Path.Combine(AppDomain.CurrentDomain.BaseDirectory, "../tools/phantomjs-1.2.0")
	root = Path.GetFullPath(root)
	phantom = "phantomjs"
	run = Path.Combine(root, "examples", "run-qunit.js")
	for file in Directory.GetFiles("test", "*.html"):
		Sh("$phantom $run $file")

task @RunTests, [@TestJs, @BuildTests]:
	index = 0
	for project in GetProjects():
		index++
		if Exist("TestResult.xml"):
			Cp("TestResult.xml", "TestResult${index}.xml")
		project.Test()

#перед запуском тестов нам нужно собрать проект, тк в проекте могут быть
#boo миграции которые используют классы из проекта
task @TryToBuild, [@PrepareBuild, @TryBeforeTest]:
	projectFile = FileSet("src/*.sln").Files.First()
	MsBuild(projectFile, "/verbosity:quiet",
			Target: "build",
			FrameworkVersion: Globals.FrameworkVersion).Execute()

task @TryBeforeTest:
	if Engine.Tasks.FirstOrDefault({t| t.Name == @BeforeTest}):
		Engine.Execute(@BeforeTest)
