import System
import System.IO
import System.Diagnostics
import System.Xml.Linq
import System.Xml.XPath.Extensions
import System.Linq.Enumerable from System.Core
import System.Collections.Generic
import file from Svn.boo

installed = List of string()
libRoot = Path.Combine(AppDomain.CurrentDomain.BaseDirectory, "../lib/")

def InstallLib(lib as string):
	if lib.Contains(" "):
		lib = lib.Replace(" ", "/")

	libPath = Path.GetFullPath(Path.Combine(libRoot, lib))
	if not Exist(libPath):
		raise "Library ${libPath} not exists"

	if not Directory.GetFiles(libPath).Length:
		maxDir as string
		for d in Directory.GetDirectories(libPath):
			name = Path.GetFileName(d)
			version = Version(name)
			if not maxDir:
				maxDir = name
			elif Version(maxDir) < version:
				maxDir = name
		lib += "/$maxDir"
	libPath = Path.Combine(libRoot, lib)

	if installed.Contains(lib):
		return

	Cp(FileSet("**.*", BaseDirectory : libPath, Excludes : ["**/.svn/**", "deps"]), "lib", true)
	installed.Add(lib)
	print "Library ${lib} installed into ${Path.GetFullPath('lib')}"
	if Exist("${libPath}/deps"):
		deps = System.IO.File.ReadAllLines("${libPath}/deps")
		for dep in deps:
			InstallLib(dep)
desc """
Устанавливает библиотеку, пример bake lib name=NUnit
"""
task @lib:
	lib = Configuration.name
	InstallLib(lib)

desc """
Выводит список доступных библиотек
"""
task @list:
	for lib in Directory.GetDirectories(libRoot):
		print Path.GetFileName(lib) unless Path.GetFileName(lib) == ".svn"

desc """
Добавляет библиотеки как сабмодули git, пример bake ImportLib libs=Common.Tools,Common.MySql
"""
task @ImportLibs:
	for lib in Configuration.libs.ToString().Split(char(',')):
		Sh("git submodule add ssh://git.analit.net/var/www/git/${lib} src/${lib}") if not Exist("src/${lib}")