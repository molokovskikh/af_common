import System
import System.IO
import Boo.Lang.Compiler
import Boo.Lang.Interpreter
import Boo.Lang.Compiler.TypeSystem.Reflection
import NHibernate
import NHibernate.AdoNet.Util
import log4net
import log4net.Appender
import log4net.Core
import log4net.Repository.Hierarchy
import Castle.ActiveRecord
import Castle.ActiveRecord.Framework

class SqlAppender(IAppender):

	property Name as string

	def Close():
		pass

	def DoAppend(message as LoggingEvent):
		formatter = BasicFormatter()
		sql = formatter.Format(message.MessageObject.ToString())
		print sql

desc """
запускает интерпритатор boo и инициализирует ActiveRecord
"""
task @DbShell, [@LoadProject, @InitActiveRecord]:
	LoggerProvider.SetLoggersFactory(Log4NetLoggerFactory())

	repository = cast(Hierarchy, LogManager.GetRepository())
	repository.Configured = true
	logger = cast(Logger, repository.GetLogger("NHibernate.SQL"))
	logger.Level = Level.Debug
	logger.AddAppender(SqlAppender(Name: "test"))

	CompilerParameters.SharedTypeSystemProvider = ReflectionTypeSystemProvider()
	interpreter = InteractiveInterpreter()
	interpreter.SetValue("scope", SessionScope())
	console = InteractiveInterpreterConsole(interpreter)
	shellFile = Path.Combine(AppDomain.CurrentDomain.BaseDirectory, "Shell.boo")
	text = File.ReadAllText(shellFile)
	console.Eval(text)
	console.Eval("import Boo.Lang.Interpreter.Builtins")
	console.Eval("import ${Globals.Project}.Models")
	console.ReadEvalPrintLoop()
